plugins {
  id 'java-library'
  id 'maven-publish'
}

ext {
  groupName = 'com.github.openseedbox'

  java_build_level = 11
  java_run_level = 11
}

publishing {
  publications {
    maven(MavenPublication) {
      from components.java
    }
  }
}

java {
  withSourcesJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(java_build_level)
  }
}
tasks.withType(JavaCompile).configureEach {
  options.release = java_run_level
}

tasks.withType(Test).configureEach {
    testLogging {
      events "passed", "skipped", "failed", "standardOut", "standardError"
    }
    javaLauncher = javaToolchains.launcherFor {
    languageVersion = JavaLanguageVersion.of(java_run_level)
  }
}

//test.onlyIf { project.hasProperty('ApplicationTestIsAlreadyDefined') }

sourceSets {
  main {
    java {
      srcDir "app"
      exclude 'views/'
    }
  }
  test {
    java {srcDir "test"}
  }
}

processResources {
  from('conf') {into 'conf' exclude 'application.conf*'}
  //from('app') {include 'play.plugins'}
  from('app/views') {into 'views'}
  from('public') {into 'public'}
}

repositories {
  mavenCentral()
}

dependencies {
  implementation("com.codeborne.replay:framework:2.1.0")
  implementation 'org.apache.commons:commons-lang3:3.12.0', {
    because 'was unexposed in commit https://github.com/codeborne/replay/commit/b6ca6b95d246'
  }
  implementation('com.ning:async-http-client:1.9.40') {
    exclude group: 'io.netty'
  }

  testImplementation('org.assertj:assertj-core:3.24.2')
  testImplementation('junit:junit:4.13.2')
}
